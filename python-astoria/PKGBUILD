# Maintainer: Student Robotics Brain Team
# Contributor: Dan Trickey
pkgname=python-astoria
pkgver=0.6.0
pkgrel=2
pkgdesc='Robot Management System'
arch=('any')
url="https://github.com/srobo/astoria"
license=('MIT')
depends=('hostapd' 'python' 'python-pydantic' 'python-click' 'python-dbus-next' 'python-gmqtt' 'python-toml' 'python-typing-extensions' 'udisks2')
makedepends=('python-setuptools' 'python-dephell')
source=("https://api.github.com/repos/srobo/astoria/tarball/v$pkgver"
        'astdiskd.service'
        'astmetad.service'
        'astprocd.service'
        'astwifid.service'
        'astoria.sysusers'
        'astoria.toml')
sha512sums=('3cd4be85a89b8db49cc96f938ddbbca6fe6a5aeaff95be121de2de79bf808275b177c07384fc030b2ef81804c0c7c2691ff2d7ac0e634775e5f0ca8155c9b5e5'
            '299d60bfa38944fca7e11caaee6b1be5820b4da977f0d75df85bfdcecd453ee3aeb03d6f9f8b77bb5a375bee4b5bfe2a2c1bf89b57de95780d9aeaa13d5480e9'
            'bc89e7f21bfef8c22901a391985e224f015b0e37db5b0f6532e041b47a7c144dce0545a5e49681db4fd4b1ff3aa021cfbde37084e7800d91c9d09c77f66c1057'
            'd133e229b29412807fcf465c33dfaded81bd8a80961ee9d8225fbaf06ef852cbbfc257eb998bb26e733df6fe83756f56af6cc6779668eef9796e5313fd3b792b'
            'a812f6e03a7cd65e11484c7fbf540cca69ab357758e7b0ef4b81086ec79d15f034b8c31002384dd07148a0632f94cae61666aad5651f9f6e0ed4afb028763509'
            '20a7f8e693b42b1e211b0c094b89e80d455019c52a7a08539f7da4344866a54b887e4f565a5698a2c34f72a256a0414117602db0b7adbefd2366a66f426f63df'
            'e488e1b5608f51a7faca6127419434d5042905ba7b3f7f1d5bda578262acdc90895988bd7739062e25c028becbb0ce2015762dd82e77883c2ba98acb3672151f')

prepare() {
  cd $(ls ${srcdir} | grep srobo-astoria)
  dephell deps convert --from pyproject.toml --to setup.py
}

package() {
  cd $(ls ${srcdir} | grep srobo-astoria)
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 ../astdiskd.service "$pkgdir/usr/lib/systemd/system/astdiskd.service"
  install -Dm644 ../astmetad.service "$pkgdir/usr/lib/systemd/system/astmetad.service"
  install -Dm644 ../astprocd.service "$pkgdir/usr/lib/systemd/system/astprocd.service"
  install -Dm644 ../astwifid.service "$pkgdir/usr/lib/systemd/system/astwifid.service"
  install -Dm644 ../astoria.sysusers "$pkgdir/usr/lib/sysusers.d/astoria.conf"
  install -Dm644 ../astoria.toml     "$pkgdir/etc/astoria.toml"
}
